apply plugin: 'war'
apply plugin: 'application'

repositories {
    jcenter()

    // Local repository for CADC private code
    maven {
        url = 'http://www.cadc-ccda.hia-iha.nrc-cnrc.gc.ca/m2repo'
    }
    mavenLocal()

}

dependencies {
    compile 'log4j:log4j:1.2.+'
    compile 'org.freemarker:freemarker:2.3.25-incubating'
    compile 'com.opencsv:opencsv:3.+'
    compile 'org.restlet.jee:org.restlet:2.3.+'
    compile 'org.restlet.jee:org.restlet.ext.servlet:2.3.+'
    compile 'org.restlet.jee:org.restlet.ext.freemarker:2.3.+'
    compile 'org.restlet.jee:org.restlet.ext.json:2.3.+'

    compile 'org.opencadc:cadc-web-util:1.+'
    compile 'org.opencadc:cadc-util:1.+'

    compile 'org.opencadc:cadc-registry:1.+'
    compile 'org.opencadc:cadc-vosi:1.+'

    compile 'org.opencadc:cadc-download-manager:1.+'
    compile 'org.opencadc:cadc-download-manager-server:1.+'
    compile 'org.opencadc:cadc-upload-manager:1.+'
    compile 'org.opencadc:cadc-vos:1.0.8+'
    compile 'commons-fileupload:commons-fileupload:1.3.+'

    compile 'org.opencadc:cadc-access-control:1.+'
    compile 'org.apache.commons:commons-configuration2:2.+'
    compile 'javax.websocket:javax.websocket-api:1.+'

    testCompile 'junit:junit:4.+'
    testCompile 'ca.nrc.cadc:web-test:1.+'
    testCompile 'org.seleniumhq.selenium:selenium-java:2.53.1'
    testCompile 'org.easymock:easymock:3.+'
}

sourceCompatibility = '1.7'
group = 'org.opencadc'
version = '1009'

def docker_image_name = 'opencadc/storage'

war {
    archiveName 'storage##' + project.version + '.war'
}

clean {
    doFirst {
        exec {
            executable 'rm'
            args('-rf', './docker')
        }
    }
}

task initDockerize(type: Copy, dependsOn: build) {
    from 'Dockerfile'
    from 'src/main/resources/LocalAuthority.properties'
    into 'docker/'
}

task copyWAR(type: Copy, dependsOn: war) {
    from 'build/libs/'
    into 'docker/'
    include '*.war'
}

task dockerize(type: Exec, dependsOn: [initDockerize, copyWAR]) {
    commandLine 'docker', 'build', '-t', docker_image_name, '-f', './docker/Dockerfile', './docker/'
}

ext {
    // Change this value to test other locations, such as Production or RC.
    intTest_default_web_app_url = 'http://mach319.cadc.dao.nrc.ca:8080'
    intTest_user_name = 'CADCtest'
    intTest_user_password = 'sywymUL4'
}

mainClassName = "ca.nrc.cadc.beacon.web.restlet.VOSpaceApplication"

run {
    if ((System.getProperty("SSO_SERVERS") != null)
            && (System.getProperty("SSO_SERVERS") != ""))
    {
        systemProperty 'SSO_SERVERS', System.getProperty("SSO_SERVERS")
    }
}

sourceSets {
    intTest
}

['firefox', 'chrome'].each { driver ->
    task "intTest${driver.capitalize()}"(type: Test) { driverTest ->
        testClassesDir = sourceSets.intTest.output.classesDir
        classpath = sourceSets.intTest.runtimeClasspath
        reports {
            html.destination = reporting.file("$name/html")
        }
        dependencies {
            intTestCompile 'junit:junit:4.+'
            intTestCompile 'org.opencadc:cadc-web-test:1.+'
            intTestCompile 'org.seleniumhq.selenium:selenium-java:2.53.1'
        }

        systemProperty "driver", "${driver}"

        if (project.hasProperty('intTest_selenium_server_url')) {
            systemProperty 'selenium.server.url', project.intTest_selenium_server_url
            logger.info('Running browser tests on ' + project.intTest_selenium_server_url)
        }
        else {
            systemProperty 'selenium.server.url', 'http://cadcint2.cadc.dao.nrc.ca:4444'
        }

        if (project.hasProperty('intTest_web_app_url')) {
            systemProperty 'web.app.url', project.intTest_web_app_url
        }
        else {
            systemProperty 'web.app.url', project.intTest_default_web_app_url
        }

        systemProperty 'user.name', project.intTest_user_name
        systemProperty 'user.password', project.intTest_user_password

        maxParallelForks = 3
    }
}
